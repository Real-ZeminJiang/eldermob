group 'chihz'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: "findbugs"
apply plugin: "jacoco"
apply plugin: 'application'


findbugs {
    toolVersion = "3.0.1"
    sourceSets = [sourceSets.main]
    ignoreFailures = true
    reportsDir = file("$project.buildDir/findbugsReports")
    effort = "max"
    reportLevel = "high"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
            'Implementation-Version': version,
            'Main-Class': 'chihz.bot.elder.API'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.alibaba', name: 'fastjson', version: '1.2.17'
    compile "com.sparkjava:spark-core:2.5"
    compile "com.lmax:disruptor:3.3.6"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

mainClassName = "chihz.bot.elder.API"
